options nodate nocenter ps=1000;
* -------------------------------------------------;
* Exemplo 8.4.1(b) da 1a edição do livro do Rencher;
* -------------------------------------------------;

data ChemReaction;
* x1=temperature x2=concentration x3=time y1=unchanged y2=converted;
input x1 x2 x3 y1 y2;
cards;
162    23.0    3.0    41.5    45.9
162    23.0    8.0    33.8    53.3
162    30.0    5.0    27.7    57.5
162    30.0    8.0    21.7    58.8
172    25.0    5.0    19.9    60.6
172    25.0    8.0    15.0    58.0
172    30.0    5.0    12.2    58.6
172    30.0    8.0     4.3    52.4
167    27.5    6.5    19.3    56.9
177    27.5    6.5     6.4    55.4
157    27.5    6.5    37.6    46.9
167    32.5    6.5    18.0    57.3
167    22.5    6.5    26.3    55.0
167    27.5    9.5     9.9    58.9
167    27.5    3.5    25.0    50.3
177    20.0    6.5    14.1    61.1
177    20.0    6.5    15.2    62.9
160    34.0    7.5    15.9    60.0
160    34.0    7.5    19.6    60.6
;

proc iml;
*pág.262;
* Outra forma de leitura dos dados: a partir de um dataset já criado;
use ChemReaction;
read all var{x1} into x1;
read all var{x2} into x2;
read all var{x3} into x3;
read all var{y1} into y1;
read all var{y2} into y2;

y = y1;
n = nrow(y);
jn = j(n,1,1);
In = I(n);
X = jn||x1||x2||x3;
k = ncol(X)-1;	* k = número de variáveis regressoras;

Beta = inv(t(X)*X)*t(X)*y;
print Beta [format=12.4];
C = {0 -2 2  0, 
     0 0  2 -1};
gl_H0 = nrow(C);
CBeta = C*Beta;
SQH0 = t(CBeta)*inv(C*(inv(t(X)*X))*t(C))*CBeta;
QMH0 = SQH0/gl_H0;

SQRes = t(y)*(In - X*inv(t(X)*X)*t(X))*y;
gl_res = n-k-1;
QMRes = SQRes/gl_res;

Fcalc = QMH0/QMRes;
p_valor = 1-cdf('F',Fcalc,gl_H0,gl_res);

print 'Exemplo 8.4.1(b): Exemplo com dados de reação química (Tabela 7.4)',,
	  'Teste H0: 2B1 = 2B2 = B3 ou H0: B1 - B2 = 2B2 - B3 = 0',;
print 'H0        ' gl_H0  SQH0[format=8.4]  QMH0[format=8.4]  Fcalc[format=8.4] p_valor[format=8.4],,
      'Resíduo   ' gl_res SQRes[format=8.4] QMRes[format=8.4],,,,;

* Testar a hipótese H0: B1=B2=B3 usando a hipótese linear geral;
C1 = {0  1 -1  0, 
      0  0  1 -1};
SQ_C1Beta = t(C1*Beta)*inv(C1*(inv(t(X)*X))*t(C1))*C1*Beta;
C2 = {0  1 -1  0, 
      0  1  0 -1};
SQ_C2Beta = t(C2*Beta)*inv(C2*(inv(t(X)*X))*t(C2))*C2*Beta;
C3 = {0  2 -1 -1, 
      0  0  1 -1};
SQ_C3Beta = t(C3*Beta)*inv(C3*(inv(t(X)*X))*t(C3))*C3*Beta;

print 'SQH0 para a hipótese H0: B1=B2=B3 usando diferentes matrizes C, em C*Beta=0',,,
     SQ_C1Beta[format=12.4] SQ_C2Beta[format=12.4]SQ_C3Beta[format=12.4];

* Teste da hipótese H0: Beta1=Beta2;
* (1) Usando C*Beta=0;
C = {0  1 -1  0};
gl_H0 = nrow(C);

CBeta = C*Beta;
SQH0 = t(CBeta)*inv(C*(inv(t(X)*X))*t(C))*CBeta;
QMH0 = SQH0/gl_H0;
Fcalc = QMH0/QMRes;
p_valor = 1-cdf('F',Fcalc,gl_H0,gl_res);

print 'Teste H0: B1 = B2 usando Hipótese Linear Geral',;
print 'H0        ' gl_H0  SQH0[format=8.4]  QMH0[format=8.4]  Fcalc[format=8.4] p_valor[format=8.4],,
      'Resíduo   ' gl_res SQRes[format=8.4] QMRes[format=8.4],,,,;

* (2) Incorporando H0: Beta1 = Beta2 ao modelo;
x12=x1+x2; * Modelo reduzido: y = B0 + B12(x1+x2) + B3x3 + e;
Xr = jn||x12||x3; * Matriz Xr do modelo reduzido;

SQH0r = t(y)*(X*inv(t(X)*X)*t(X)-Xr*inv(t(Xr)*Xr)*t(Xr))*y;
print 'SQ de H0:B1=B2, usando modelo completo x modelo reduzido:'
    ,,SQH0r[format=12.4]; 

quit;


* >>> Solução utilizando o proc reg <<<;
proc reg data=ChemReaction;
 model y1 = x1 x2 x3;
* test x1=0,x2=0,x3=0;
* test x1=x2,2*x2=x3;
* test x1=x2, x1=x3;
 test x1=x2;
run;
